attribute float a_point_idx;
attribute float a_self_rotation;
attribute vec3 a_base_triangle_normal;
attribute vec3 a_base_position;

uniform mat4 u_planet_modelview_matrix;
uniform mat4 u_projection_matrix;
uniform vec2 u_grass_size;  // (width, height)

varying vec2 v_tex_coord;
varying vec3 v_position;

void main() {
  // 4 kinds of points:
  // 2 ----- 1   | idx |       coords |    tex |
  // |       |   |   0 |  (w/2, 0, 0) | (1, 1) |
  // 3 ----- 0   |   1 |  (w/2, h, 0) | (1, 0) |
  //             |   2 | (-w/2, h, 0) | (0, 0) |
  //             |   3 | (-w/2, 0, 0) | (0, 1) |
  vec3 position;
  float x;
  if (a_point_idx == 0.0 || a_point_idx == 1.0) {
    x = u_grass_size.x / 2.0;
    if (a_point_idx == 0.0) {
      position.y = 0.0;
      v_tex_coord = vec2(1.0, 1.0);
    } else {
      position.y = u_grass_size.y;
      v_tex_coord = vec2(1.0, 0.0);
    }
  } else {
    x = -u_grass_size.x / 2.0;
    if (a_point_idx == 2.0) {
      position.y = u_grass_size.y;
      v_tex_coord = vec2(0.0, 0.0);
    } else {
      position.y = 0.0;
      v_tex_coord = vec2(0.0, 1.0);
    }
  }
  position.x = x * cos(radians(a_self_rotation));
  position.z = -x * sin(radians(a_self_rotation));

  // Let (nx, ny, nz) - normal.
  // Wee need to find to vectors, (ax, ay, az) and (ox, oy, oz), orthogonal to
  // normal and to each other.
  // 1. ax*nx + ay*ny + az*nz = 0 => (ax, ay, az) = (-nz, 0, nx)
  // 2. ox*nx + oy*ny + oz*nz = 0     ox*nx + oy*ny + oz*nz = 0
  //    ox*ax + oy*ay + oz*az = 0 => -ox*nz +       + oz*nx = 0 =>
  //    (ox, oy, oz) = (nx*ny, -nx*nx-nz*nz, ny*nz)
  vec3 normal = normalize(a_base_triangle_normal);
  vec3 abscissa = normalize( vec3(-normal.z, 0.0, normal.x) );
  vec3 ordinate = normalize( vec3(normal.x * normal.y,
                                  -normal.x * normal.x - normal.z * normal.z,
                                  normal.y * normal.z) );
  // ox oy oz 0
  // nx ny nz 0
  // ax ay az 0
  // px py pz 1
  mat4 modelview_matrix = mat4(vec4(ordinate, 0.0), vec4(normal, 0.0),
                               vec4(abscissa, 0.0), vec4(a_base_position, 1.0));
  gl_Position = u_planet_modelview_matrix * vec4(position, 1.0);
  gl_Position = modelview_matrix * gl_Position;
  v_position = vec3(gl_Position);
  gl_Position = u_projection_matrix * gl_Position;
}
