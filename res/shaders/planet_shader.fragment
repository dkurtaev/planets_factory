uniform sampler2D u_texture;
// Sun shading.
uniform vec3 u_sun_position;
uniform float u_sun_radius;
uniform vec3 u_planet_position;
uniform float u_planet_radius;
uniform bool u_sun_shading;
// Highlighting ring.
uniform bool u_process_touch;
uniform float u_touch_angle;
uniform vec3 u_touch_coord;

varying vec3 v_normal;
varying vec2 v_tex_coord;
varying vec3 v_position;

void SunShading(vec3 light_vector, vec4 n_light_vector) {
  // Check sun covering.
  //                      ___0____
  //                     /   |  / \
  //                    /    | /   \
  //                 1 |_____|/_____| -1
  //                   | cover\     |
  //                    \angle \   /
  //                     \___|__\_/
  //                         0
  //   <-- sun             planet

  float distance_to_sun = sqrt(dot(light_vector, light_vector));
  vec4 n_position = vec4( normalize(v_position - u_planet_position), 0.0f );

  // cos(cover_angle) = (r - R) / d. Value in [-1, 0];
  float sun_cover = (u_planet_radius - u_sun_radius) / distance_to_sun;
  float fragment_cover_value = dot(n_position, n_light_vector);  // in [-1, 1]

  fragment_cover_value = max(fragment_cover_value, sun_cover);
  float sun_power = (fragment_cover_value - sun_cover) / (1.0f - sun_cover);
  gl_FragColor *= sqrt(sun_power);
  gl_FragColor[3] = 1.0f;
}

void HighlightingRing() {
  float c_highlighting_internal_ring_radius = 0.8f;  // in [0, 1]
  vec4 c_highlighting_color = vec4(0.0f, 0.8f, 0.0f, 0.3f);

  float angle = acos(dot( normalize(v_position), normalize(u_touch_coord) ));
  if (c_highlighting_internal_ring_radius * u_touch_angle <= angle &&
      angle <= u_touch_angle) {
    float alpha = c_highlighting_color[3];
    gl_FragColor = (1.0f - alpha) * gl_FragColor +
                   alpha * c_highlighting_color;
  }
}

void main() {
  vec3 light_vector = u_sun_position - u_planet_position;
  vec4 n_light_vector = vec4( normalize(light_vector), 0.0f);
  vec4 n_normal = vec4( normalize(v_normal), 0.0f);

  float lum_value = (dot(n_light_vector, n_normal) + 2.0f) / 3.0f;
  gl_FragColor = texture2D(u_texture, v_tex_coord) * lum_value;

  if (u_sun_shading) {
    SunShading(light_vector, n_light_vector);
  }
  if (u_process_touch) {
    HighlightingRing();
  }
}
